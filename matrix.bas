001 REM A PROGRAM THAT DRAWS THE MATRIX CODE STREAMER THINGYS ON THE SCREEN
002 REM BUT SINCE THIS IS WRITTEN IN BASIC IT IS SUUUUUUUUUUUUUUUUUUUUPER
003 REM SLOW.

010 IF PEEK($D9) <> 80 THEN SYS $FF5F :REM SWITCH TO 80 CHARACTER MODE
020 VPOKE $F, $2000, $01 :REM SETS COLOUR MODE TO MODE-0 (DEFAULT)
030 CODENUM = 128 :REM HOW MANY CODE STREAMERS TO HAVE X>0

100 REM BLANKS THE SCREEN
110 FOR Y = 0 TO 60-1
120   FOR X = 0 TO 80-1
130     VPOKE 0, Y*256 + X*2, $20
140     VPOKE 0, Y*256 + X*2 + 1, 0
150   NEXT X
160 NEXT Y

200 REM SETS 16 COLOUR PALLET TO GREEN SCALE
210 FOR I = 0 TO 15
220   VPOKE $F, $1000 + I*2, I*16
230   VPOKE $F, $1000 + I*2 + 1, 0
240 NEXT

300 REM SETS UP 'STREAMERS' DATA STRUCTURES
310 AMOUNT = CODENUM - 1
320 DIM CODE%(AMOUNT, 1) :REM STORES X, Y VALUES FOR STREAMERS
330 DIM ACTIVE(AMOUNT) :REM A SIMPLE TRUE/FALSE FOR IF A STREAMER IS ACTIVE

500 REM GENERATES NEW STREAMERS WHEN THERE IS AN AVAILABLE OPENING
510 IF RND(1) > 0.75 THEN GOTO 600 :REM 1/2 CHANCE TO SPAWN NEW STREAMERS
520 FOR I = 0 TO AMOUNT
530   IF ACTIVE(I) > 0 THEN GOTO 580
540   ACTIVE(I) = 1
550   CODE%(I, 0) = INT(RND(1)*80)
560   CODE%(I, 1) = 0
570   I = 500
580 NEXT I

600 FOR I = 0 TO AMOUNT
610   IF ACTIVE(I) = O THEN 740 :REM SKIPS INACTIVE STREAMER
620   X = CODE%(I, 0) :REM GENERATES NEW CHARACTER
630   Y = CODE%(I, 1)
640   VPOKE 0, Y*256 + X*2, INT(RND(1)*128)+32
650   VPOKE 0, Y*256 + X*2 + 1, $0F
660   IF RND(1)*4 > 3 THEN VPOKE 0, Y*256 + X*2 + 1, $F0 :REM 1/4 INVERT CHANCE
670   FOR J = Y - 16 TO Y - (16+16) STEP -1 :REM DIMS THE CHARACTERS ABOVE IT
680     IF J < 0 OR J > 80 THEN GOTO 720
690     OLD = VPEEK(0, J*256 + X*2 + 1)
700     IF OLD => 16 THEN VPOKE 0, J*256 + X*2 + 1, OLD - 16
710     IF OLD > 0 AND OLD < 16 THEN VPOKE 0, J*256 + X*2 + 1, OLD - 1
720   NEXT J
730   CODE%(I, 1) = Y + 1
740 NEXT I

800 REM MARKS STREAMERS INACTIVE
810 FOR I = 0 TO AMOUNT
820   IF CODE%(I, 1) > 60+16+16 THEN ACTIVE(I) = 0
830 NEXT
840 GOTO 500

